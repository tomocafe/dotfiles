set backspace=2

set tabstop=4
set shiftwidth=4
set softtabstop=4
set expandtab
set smarttab

" Add site-specific indent settings in ~/.vim/plugins
:au FileType make setlocal noexpandtab

set laststatus=2
set cursorline
set hlsearch
set showmatch
set matchtime=0
syntax enable

set number
set relativenumber

nmap <F2> <Esc>:noh<CR>
nmap <F3> <Esc>:set nu! rnu!<CR>
nnoremap Q <nop>

" *last-position-jump*
:au BufReadPost * if line("'\"") > 1 && line("'\"") <= line("$") | exe "normal! g`\"" | endif

set backup
set swapfile
set undofile
if !isdirectory($HOME."/.vim/backups")
    silent! execute "!mkdir ~/.vim/backups"
endif
set backupdir=~/.vim/backups//
if !isdirectory($HOME."/.vim/swaps")
    silent! execute "!mkdir ~/.vim/swaps"
endif
set directory=~/.vim/swaps//
if !isdirectory($HOME."/.vim/undos")
    silent! execute "!mkdir ~/.vim/undos"
endif
set undodir=~/.vim/undos//

" Disable swap/etc on large files (>10M)
let g:LargeFileSz = 10 * 1024 * 1024
augroup LargeFile
    au!
    autocmd BufReadPre * let sz=getfsize(expand("<afile>")) | if sz > g:LargeFileSz || sz == -2 | call LargeFileMode() | endif
augroup END
function! LargeFileMode()
    " Disable backup, swap, and undo; unload hidden buffers for memory savings
    setlocal nobackup
    setlocal noswapfile
    setlocal undolevels=-1
    setlocal bufhidden=unload
    autocmd VimEnter * echo "Large file mode activated"
endfunction

" Source theme (invokes colorscheme)
try
    source ~/.vim/theme
catch
endtry

" Status line
" User1-9, {Def,Ro,Mod}Header defined in _theme.m4
function! StatusLineHeader()
    return '  '.bufnr('%').' '
endfunction
set statusline=
set statusline+=%#DefHeader#%{&mod?'':&readonly?'':StatusLineHeader()} "default header
set statusline+=%#RoHeader#%{&mod?'':&readonly?StatusLineHeader():''} "readonly header
set statusline+=%#ModHeader#%{&mod?StatusLineHeader():''} "modified header
set statusline+=%1*\ %t "basename
set statusline+=%3*\ %y "filetype
set statusline+=%4*\ %{''.(&fenc!=''?&fenc:&enc).''}/%{&ff} "encoding/fileformat
set statusline+=%1*%= "right justify
set statusline+=%5*x%B "character code under cursor
set statusline+=%2*\ %c,%l/%L "col/row/total
set statusline+=%6*\ %p%% "line%

