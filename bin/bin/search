#!/usr/bin/env perl

use warnings;
use strict;

use Term::ANSIColor;
use File::stat;
use Cwd;

###
### |defaults|
###

my $ENGINE = "ack";
my $ENGINEOPTS = $ENV{'SEARCH_ENGINEOPTS'}; # default is SEARCH_<ENGINE>_OPTIONS environment variable, e.g. SEARCH_ACK_OPTIONS
my $PAGER = "less -SFXRM";
my $EDITOR = $ENV{'EDITOR'} ? $ENV{'EDITOR'} : 'vim';
my $ACTION = "$EDITOR {} +{}";
my $CACHEDIR = $ENV{'SEARCH_CACHEDIR'} ? $ENV{'SEARCH_CACHEDIR'} : "$ENV{'HOME'}/.cache/search";
my $CACHESIZE = $ENV{'SEARCH_CACHESIZE'} ? $ENV{'SEARCH_CACHESIZE'} : 10;
my $WSL = exists $ENV{'WSLENV'};
my $NTEXE = 0;

###
### |subroutines|
###

# Retrieve all cache files sorted descending by modification time
sub getcaches {
    mkdir $CACHEDIR unless -d $CACHEDIR or die $!;
    opendir (my $D, $CACHEDIR) or die $!;
    my @files = grep { $_ !~ m/^\.+$/ } readdir($D);
    @files = sort { (stat "$CACHEDIR/$b")->[10] <=> (stat "$CACHEDIR/$a")->[10] } @files;
    return @files;
}

# Set up the cache file for later recall
# Generates a cache file name with the timestamp and truncated encoded arguments
sub setcache {
    # Clean up the oldest cache file once the cache overflows
    my @old = getcaches ();
    while (scalar @old >= $CACHESIZE) {
        my $rem = pop @old;
        unlink "$CACHEDIR/$rem" or warn "Failed to clean up cache: $rem";
    }
    # Encode and truncate arguments as the new cache file name
    my $encoded = join '%20', @_;
    $encoded =~ s/ /%20/g;
    $encoded =~ s/\*/%2a/g;
    $encoded =~ s/\//%2f/g;
    my $maxlen = 40;
    for (my $offset = 2; $offset > 0; $offset--) {
        if (length($encoded) >= $maxlen - $offset + 3 && 
            substr($encoded, $maxlen - $offset, 3) =~ m/%2[0af]/) {
            $maxlen -= $offset;
            last;
        }
    }
    my $cache = $CACHEDIR . "/" . time() . ":" . substr($encoded, 0, $maxlen);
    return $cache;
}

# Increment the index in base-36
sub b36inc {
    my ($p, $d) = (substr($_[0], 0, length($_[0]) - 1), substr ($_[0], -1));
    if    ($d eq '9') { $_[0] =        $p  . "a";  }
    elsif ($d eq 'k') { $_[0] =        $p  . "m";  } # skip L for legibility
    elsif ($d eq 'z') { $_[0] = b36inc($p) . "0";  }
    else              { $_[0] =        $p  . ++$d; }
}

# Strip ANSI color codes from the string
# This should be replaced by Term::ANSIColor but it requires v2.01+
# ag uses \e[K (clear to end of line) which also needs stripping
sub colorstrip {
    my $dirty = shift @_;
    (my $clean = $dirty) =~ s/\e\[(\d+(?>(;\d+)*)m|K)//g;
    return $clean;
}

# Trim leading/trailing whitespace from matching line
# This makes it easier to view all the results together, since each
# matching line might have different indentation levels
sub trimws {
    my $line = shift @_;
    $line =~ s/:\s*/: /;
    $line =~ s/\s*$//;
    return $line;
}

# Captures search engine output and pipes to pager, appending indices in front of matches
sub search {
    # Open a pipe to invoke the search engine
    my @args = map { "\"$_\"" } @_; # re-quote all arguments to pass to the engine
    my $envoptvar = "SEARCH_" . uc($ENGINE) . "_OPTIONS";
    my $envopts = exists $ENV{$envoptvar} ? $ENV{$envoptvar} : "";
    my $opts = $ENGINEOPTS ? $ENGINEOPTS : $envopts;
    open (my $I, '-|', "$ENGINE --group --color $opts @args") or die $!;
    my $O = undef;
    my $F = undef;
    my $id = 0;
    # For each line of the search engine output (stdout)
    while (my $line = <$I>) {
        # Append a base-36 index in front of matches
        # and clean up leading whitespace
        if (colorstrip($line) =~ m/^[0-9]+:/) { # is a match
            b36inc $id; # start with 1 since it is easier to reach than 0 on the keyboard
            $line = sprintf "%s%-3s%s %s\n", color('cyan'), $id, color('reset'), trimws($line);
        }
        # Pipe to the output pager
        unless ($O) {
            open ($O, "|$PAGER") or die $!;
        }
        print $O "$line";
        # Write to the cache
        unless ($F) {
            open ($F, '>', setcache(@_)) or die $!;
            # Print a header to the cache file
            print $F "::@_\n";
            print $F "::" . cwd . "\n";
            print $F "::" . scalar localtime() . "\n";
        }
        print $F "$line";
    }
    close $I;
    close $O if $O;
    close $F if $F;
}

# List caches available (with search arguments, working directory, time and date of search)
sub listcaches {
    my $id = 0;
    foreach my $cache (getcaches ()) {
        open (my $fd, '<', "$CACHEDIR/$cache") or die $!;
        my @header = ();
        while (my $line = <$fd>) {
            last unless $line =~ m/^::/;
            $line =~ s/^:://;
            push @header, $line;
        }
        close $fd;
        my $w = length($CACHESIZE); # +1 for leading '+'
        printf "%s%${w}s%s %s%s%s", color('cyan'), '+' . $id, color('reset'), color('green'), $header[0], color('reset');
        for (my $i = 1; $i < scalar @header; $i++) {
            printf "%${w}s %s", ' ', $header[$i];
        }
        $id++;
    }
}

# Given a cache, re-list the matches
sub listmatches {
    my $cacheid = shift @_;
    my $cache = (getcaches)[$cacheid];
    return unless $cache;
    open (my $I, '<', "$CACHEDIR/$cache") or die $!;
    open (my $O, "|$PAGER") or die $!;
    while (my $line = <$I>) {
        next if $line =~ m/^::/; # skip headers
        print $O "$line";
    }
}

sub wslpath {
    my $lpath = shift @_;
    my $wpath = `wslpath -w "$lpath"`;
    chomp $wpath;
    return $wpath;
}

# Invoke the action (e.g. editor) on the selected match
sub openselection {
    my ($filepath, $lineno) = @_;
    my $cmd = $ACTION;
    $filepath = wslpath "$filepath" if $WSL and $NTEXE;
    $cmd =~ s/{file}/"$filepath"/g;
    $cmd =~ s/{line}/$lineno/g;
    $cmd =~ s/{}/"$filepath"/; # replace first occurrence of {} with filepath
    $cmd =~ s/{}/$lineno/; # replace second occurrence of {} with lineno
    exec $ENV{'SHELL'}, "-c", $cmd;  # using this instead of system honors user's shell instead of using /bin/sh
}

# Select a match by index from the given cache
sub selectmatch {
    my ($id, $cacheid) = @_;
    my $cache = (getcaches)[$cacheid];
    return unless $cache;
    my @header = ();
    my $curfile = '';
    open (my $I, '<', "$CACHEDIR/$cache") or die $!;
    while (my $line = <$I>) {
        chomp $line;
        $line = colorstrip($line);
        if ($line =~ m/^::/) {
            $line =~ s/^:://;
            push @header, $line;
        }
        elsif ($line =~ m/^[0-9a-z]+\s+[0-9]+:/) {
            if (my ($lineno) = ($line =~ m/^${id}\s+([0-9]+):/)) {
                openselection "$header[1]/$curfile", $lineno;
                last;
            }
        }
        elsif ($line !~ m/^$/) {
            $curfile = $line;
        } 
    }
}

###
### |main|
###

# Interpret executable name (symlink) to select action and parameters
if ($0 =~ m/ack/)   { $ENGINE = "ack"; }
elsif ($0 =~ m/ag/) { $ENGINE = "ag";  }

# Adjust default action for editors that do not follow +lineno syntax
if ($EDITOR =~ m/code/) {
    $ACTION = "$EDITOR -g {}:{}";
}

if ($0 =~ m/list-cache/) {
    listcaches;
}
elsif ($0 =~ m/select/) {
    # Check for specific cache ID
    my $cache = 0;
    while (scalar @ARGV and $ARGV[0] =~ m/^[+%][0-9]+$/) {
        ($cache = $ARGV[0]) =~ s/^[+%]//;
        if (substr($ARGV[0], 0, 1) eq '%') {
            # Touch the cache file and reset cache ID to 0
            if (system('touch', "$CACHEDIR/" . (getcaches)[$cache]) == 0) {
                $cache = 0;
            }
        }
        shift @ARGV;
    }
    # Check for other flags
    while (scalar @ARGV and $ARGV[0] =~ /^-/) {
        if ($ARGV[0] eq '--') {
            shift @ARGV;
            last;
        }
        if ($ARGV[0] eq '--wsl-exe') {
            $NTEXE = 1;
            shift @ARGV;
        }
    }
    # Set action if specified
    if ($ARGV[1]) {
        $ACTION = "$ARGV[1]";
    }
    # No selection, re-list all matches in the cache
    if (scalar @ARGV == 0) {
        listmatches $cache;
    }
    # Open the selected match
    else {
        selectmatch $ARGV[0], $cache;
    }
}
else {
    search (@ARGV);
}

